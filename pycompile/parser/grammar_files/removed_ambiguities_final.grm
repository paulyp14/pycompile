<START> ::= <prog>

<addOp> ::= '+'
<addOp> ::= '-'
<addOp> ::= 'or'

<aParams> ::= <expr> <rept-aParams1>
<aParams> ::= EPSILON

<aParamsTail> ::= ',' <expr>

<arithExpr> ::= <term> <rightrec-arithExpr>

<term> ::= <factor> <rightrec-termNoRel>

<termNoRel> ::= <factorNoRel> <rightrec-termNoRel>

<expr> ::= <arithExpr>
<relExpr> ::= <arithExprNoRel> <relOp> <arithExprNoRel>

<rightrec-arithExpr> ::= EPSILON
<rightrec-arithExpr> ::= <addOp> <term> <rightrec-arithExpr>
<rightrec-arithExpr> ::= <relOp> <termNoRel> <rightrec-arithExprNoRel>

<rightrec-arithExprNoRel> ::= EPSILON
<rightrec-arithExprNoRel> ::= <addOp> <termNoRel> <rightrec-arithExprNoRel>

<rightrec-termNoRel> ::= EPSILON
<rightrec-termNoRel> ::= <multOp> <factorNoRel> <rightrec-termNoRel>

<arithExprNoRel> ::= <termNoRel> <rightrec-arithExprNoRel>

<factor> ::= <funcVarStart>
<factor> ::= 'intLit'
<factor> ::= 'floatLit'
<factor> ::= 'stringLit'
<factor> ::= '(' <arithExprNoRel> ')'
<factor> ::= 'not' <factor>
<factor> ::= <sign> <factor>
<factor> ::= 'qm' '[' <expr> ':' <expr> ':' <expr> ']'

<factorNoRel> ::= <funcVarStart>
<factorNoRel> ::= 'intLit'
<factorNoRel> ::= 'floatLit'
<factorNoRel> ::= 'stringLit'
<factorNoRel> ::= '(' <arithExprNoRel> ')'
<factorNoRel> ::= 'not' <factorNoRel>
<factorNoRel> ::= <sign> <factorNoRel>
<factorNoRel> ::= 'qm' '[' <expr> ':' <expr> ':' <expr> ']'

<arraySize> ::= '[' <arraySizeEnd>

<arraySizeEnd> ::=  'intNum' ']'
<arraySizeEnd> ::= ']'

<assignOp> ::= '='

<functionCall> ::= '(' <aParams> ')'

<variable> ::= <rept-variable2>

<funcVarStart> ::= 'id' <funcVarRept1>

<funcVarRept1> ::= <idNestEnd> <funcVarReptOrEnd1>
<funcVarReptOrEnd1> ::= <variable>
<funcVarReptOrEnd1> ::= <functionCall>
<funcVarReptOrEnd1> ::= 'id' <funcVarRept1>

<funcVarRept2> ::= <idNestEnd> <funcVarReptOrEnd2>
<funcVarReptOrEnd2> ::= <variable> <assignOp> <expr>
<funcVarReptOrEnd2> ::= <functionCall>
<funcVarReptOrEnd2> ::= 'id' <funcVarRept2>

<assignFuncStart> ::= 'id' <funcVarRept2>

<classDecl> ::= 'class' 'id' <opt-classDecl2> '{' <rept-classDecl4> '}' ';'

<fParams> ::= <type> 'id' <rept-fParams2> <rept-fParams3>
<fParams> ::= EPSILON

<fParamsTail> ::= ',' <type> 'id' <rept-fParamsTail3>

<funcBody> ::= '{' <opt-funcBody1> <rept-funcBody2> '}'

<funcDecl> ::= 'func' 'id' '(' <fParams> ')' <funcMid> ';'

<funcDef> ::= <funcHead> <funcBody>

<funcHead> ::= 'func' <opt-funcHead1> '(' <fParams> ')' <funcMid>

<funcMid> ::= ':' <funcEnd>

<funcEnd> ::= 'void'
<funcEnd> ::= <type>

<idnestEnd> ::= <rept-idnest1> '.'
<idnestEnd> ::= '(' <aParams> ')' '.'

<indice> ::= '[' <arithExprNoRel> ']'

<memberDecl> ::= <funcDecl>
<memberDecl> ::= <varDecl>

<multOp> ::= '*'
<multOp> ::= '/'
<multOp> ::= 'and'

<opt-classDecl2> ::= 'inherits' 'id' <rept-opt-classDecl22>
<opt-classDecl2> ::= EPSILON

<opt-funcBody1> ::= 'var' '{' <rept-opt-funcBody12> '}'
<opt-funcBody1> ::= EPSILON

<opt-funcHead1> ::= 'id' <opt-funcHeadEnd>
<opt-funcHeadEnd> ::= 'sr' 'id'
<opt-funcHeadEnd> ::= EPSILON

<prog> ::= <rept-prog0> <rept-prog1> 'main' <funcBody>

<relOp> ::= 'eq'
<relOp> ::= 'neq'
<relOp> ::= 'lt'
<relOp> ::= 'gt'
<relOp> ::= 'leq'
<relOp> ::= 'geq'

<rept-aParams1> ::= <aParamsTail> <rept-aParams1>
<rept-aParams1> ::= EPSILON

<rept-classDecl4> ::= <visibility> <memberDecl> <rept-classDecl4>
<rept-classDecl4> ::= EPSILON

<rept-fParams2> ::= <arraySize> <rept-fParams2>
<rept-fParams2> ::= EPSILON

<rept-fParams3> ::= <fParamsTail> <rept-fParams3>
<rept-fParams3> ::= EPSILON

<rept-fParamsTail3> ::= <arraySize> <rept-fParamsTail3>
<rept-fParamsTail3> ::= EPSILON

<rept-funcBody2> ::= <statement> <rept-funcBody2>
<rept-funcBody2> ::= EPSILON


<rept-idnest1> ::= <indice> <rept-idnest1>
<rept-idnest1> ::= EPSILON

<rept-opt-classDecl22> ::= ',' 'id' <rept-opt-classDecl22>
<rept-opt-classDecl22> ::= EPSILON

<rept-opt-funcBody12> ::= <varDecl> <rept-opt-funcBody12>
<rept-opt-funcBody12> ::= EPSILON

<rept-prog0> ::= <classDecl> <rept-prog0>
<rept-prog0> ::= EPSILON

<rept-prog1> ::= <funcDef> <rept-prog1>
<rept-prog1> ::= EPSILON

<rept-statBlock1> ::= <statement> <rept-statBlock1>
<rept-statBlock1> ::= EPSILON

<rept-varDecl2> ::= <arraySize> <rept-varDecl2>
<rept-varDecl2> ::= EPSILON

<rept-variable2> ::= <indice> <rept-variable2>
<rept-variable2> ::= EPSILON

<sign> ::= '+'
<sign> ::= '-'

<statBlock> ::= '{' <rept-statBlock1> '}'
<statBlock> ::= <statement>
<statBlock> ::= EPSILON

<statement> ::= 'if' '(' <relExpr> ')' 'then' <statBlock> 'else' <statBlock> ';'
<statement> ::= 'while' '(' <relExpr> ')' <statBlock> ';'
<statement> ::= 'read' '(' <statVariable> ';'
<statement> ::= 'write' '(' <expr> ')' ';'
<statement> ::= 'return' '(' <expr> ')' ';'
<statement> ::= 'break' ';'
<statement> ::= 'continue' ';'
<statement> ::= <assignFuncStart> ';'

<statVariable> ::= 'id' <statVarRept> <statVarEnd>
<statVarRept> ::= '(' <aParams> ')' '.' <statVarRept2>
<statVarRept> ::= <indiceRept>
<statVarRept2> ::= 'id' <statVarRept>
<indiceRept> ::= '[' <arithExprNoRel> ']' <indiceRept>
<indiceRept> ::= '.' <statVarRept2>
<indiceRept> ::= ')'
<statVarEnd> ::= '[' <arithExprNoRel> ']' <statVarEnd>
<statVarEnd> ::= EPSILON

<type> ::= 'integer'
<type> ::= 'float'
<type> ::= 'string'
<type> ::= 'id'

<varDecl> ::= <type> 'id' <rept-varDecl2> ';'

<visibility> ::= 'public'
<visibility> ::= 'private'
<visibility> ::= EPSILON


